<hr><h2 id=title-levenshtein-distance-algorithm-erlang-implementation>title: Levenshtein Distance Algorithm: Erlang Implementation</h2><p>by Fredrik Svensson and Adam Lindberg</p><pre><code>%%%-------------------------------------------------------------------
%%% File:        string_metrics.erl
%%%
%%% Author:        Fredrik Svensson
%%%             Adam Lindberg
%%%-------------------------------------------------------------------
-module(string_metrics).

-export([levenshtein/2]).

%% Calculates the Levenshtein distance between two strings
levenshtein(Samestring, Samestring) -&gt; 0;
levenshtein(String, []) -&gt; length(String);
levenshtein([], String) -&gt; length(String);
levenshtein(Source, Target) -&gt;
    levenshtein_rec(Source, Target, lists:seq(0, length(Target)), 1).

%% Recurses over every character in the source string and calculates a list of distances
levenshtein_rec([SrcHead|SrcTail], Target, DistList, Step) -&gt;
    levenshtein_rec(SrcTail, Target, levenshtein_distlist(Target, DistList, SrcHead, [Step], Step), Step + 1);
levenshtein_rec([], _, DistList, _) -&gt;
    lists:last(DistList).

%% Generates a distance list with distance values for every character in the target string
levenshtein_distlist([TargetHead|TargetTail], [DLH|DLT], SourceChar, NewDistList, LastDist) when length(DLT) &gt; 0 -&gt;
    Min = lists:min([LastDist + 1, hd(DLT) + 1, DLH + dif(TargetHead, SourceChar)]),
    levenshtein_distlist(TargetTail, DLT, SourceChar, NewDistList ++ [Min], Min);
levenshtein_distlist([], _, _, NewDistList, _) -&gt;
    NewDistList.

% Calculates the difference between two characters or other values
dif(C, C) -&gt; 0;
dif(_C1, _C2) -&gt; 1.
</code></pre>